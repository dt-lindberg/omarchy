import QtQuick

// Averages the colour of the strip a bar covers, from a wallpaper image the
// background has already decoded. Decoding the file again in ImageMagick took
// 0.3-1.2s for large wallpapers; this reads back only the strip.
Item {
  id: root

  property var pendingCallback: null
  property string grabUrl: ""
  property rect stripRect: Qt.rect(0, 0, 0, 0)

  // The grab renders on the GPU. The software renderer returns empty strips,
  // so callers fall back to decoding the file instead.
  readonly property bool available: GraphicsInfo.api !== GraphicsInfo.Software

  function barRect(position, barSize, width, height) {
    var size = Math.min(barSize, position === "top" || position === "bottom" ? height : width)
    if (position === "top") return Qt.rect(0, 0, width, size)
    if (position === "bottom") return Qt.rect(0, height - size, width, size)
    if (position === "left") return Qt.rect(0, 0, size, height)
    return Qt.rect(width - size, 0, size, height)
  }

  // Calls back with "#rrggbb", or "" when the strip could not be read.
  function sample(image, position, barSize, callback) {
    var rect = barRect(position, barSize, image.width, image.height)
    if (!available || rect.width < 1 || rect.height < 1) {
      callback("")
      return
    }
    // A newer request replaces one still in flight; its caller has moved on.
    pendingCallback = callback
    stripRect = rect
    strip.sourceItem = image
    strip.sourceRect = rect
    strip.width = rect.width
    strip.height = rect.height
    strip.scheduleUpdate()
    var requested = callback
    var ok = strip.grabToImage(function(result) {
      if (root.pendingCallback !== requested) return
      root.grabUrl = result.url
      canvas.width = rect.width
      canvas.height = rect.height
      canvas.loadImage(result.url)
      if (canvas.isImageLoaded(result.url)) canvas.requestPaint()
    })
    if (!ok) finish("")
  }

  // Runs from the canvas's paint, the first point where a resized canvas has a
  // buffer of its new size; reading straight after a resize returned black.
  function average() {
    if (!pendingCallback || !grabUrl || !canvas.isImageLoaded(grabUrl)) return
    var w = stripRect.width, h = stripRect.height
    var ctx = canvas.getContext("2d")
    ctx.clearRect(0, 0, w, h)
    // The grab arrives in physical pixels (strip size x device pixel ratio),
    // so draw it scaled to the strip's own size before reading it back.
    ctx.drawImage(grabUrl, 0, 0, w, h)
    var data = ctx.getImageData(0, 0, w, h).data
    // A still covers the canvas, but a video wallpaper would show it.
    ctx.clearRect(0, 0, w, h)
    var red = 0, green = 0, blue = 0
    for (var i = 0; i < data.length; i += 4) {
      red += data[i]
      green += data[i + 1]
      blue += data[i + 2]
    }
    canvas.unloadImage(grabUrl)
    grabUrl = ""
    var count = w * h
    function channel(sum) {
      var hex = Math.floor(sum / count).toString(16)
      return hex.length < 2 ? "0" + hex : hex
    }
    finish("#" + channel(red) + channel(green) + channel(blue))
  }

  function finish(value) {
    var callback = pendingCallback
    pendingCallback = null
    strip.sourceItem = null
    strip.width = 0
    strip.height = 0
    if (callback) callback(value)
  }

  ShaderEffectSource {
    id: strip
    live: false
    hideSource: false
  }

  Canvas {
    id: canvas
    width: 1
    height: 1
    renderTarget: Canvas.Image
    renderStrategy: Canvas.Immediate
    onImageLoaded: requestPaint()
    onPaint: root.average()
  }
}
